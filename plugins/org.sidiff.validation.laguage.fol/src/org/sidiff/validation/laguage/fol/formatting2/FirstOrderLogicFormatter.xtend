/*
 * generated by Xtext 2.16.0
 */
package org.sidiff.validation.laguage.fol.formatting2

import org.eclipse.xtext.formatting2.AbstractFormatter2
import org.eclipse.xtext.formatting2.IFormattableDocument
import org.sidiff.validation.laguage.fol.firstOrderLogic.Constraint
import org.sidiff.validation.laguage.fol.firstOrderLogic.ConstraintLibrary
import org.sidiff.validation.laguage.fol.firstOrderLogic.Domain
import org.sidiff.validation.laguage.fol.firstOrderLogic.FirstOrderLogicPackage
import org.sidiff.validation.laguage.fol.firstOrderLogic.Get

class FirstOrderLogicFormatter extends AbstractFormatter2 {
	
	def dispatch void format(ConstraintLibrary constraintLibrary, extension IFormattableDocument document) {
		constraintLibrary.domains.forEach[format]
		constraintLibrary.constraints.forEach[format]
	}

	def dispatch void format(Domain domain, extension IFormattableDocument document) {
		domain.prepend[noIndentation].prepend[noSpace]
		domain.regionFor.feature(FirstOrderLogicPackage.Literals.DOMAIN__DOMAIN).prepend[oneSpace]
		domain.append[newLine]
	}

	def dispatch void format(Constraint constraint, extension IFormattableDocument document) {
		constraint.prepend[newLines = 2 highPriority]
		constraint.allRegionsFor.features(
			FirstOrderLogicPackage.Literals.CONSTRAINT__NAME,
			FirstOrderLogicPackage.Literals.CONSTRAINT__MESSAGE).forEach[append[newLine]]
		constraint.regionFor.feature(FirstOrderLogicPackage.Literals.CONSTRAINT__MESSAGE).prepend[oneSpace]
		
		constraint.variable.surround[oneSpace]
		
		constraint.formula.allRegionsFor.keywords('or', 'and', 'implies', 'xor', '=').forEach[surround[newLine]]
		constraint.formula.allRegionsFor.keywords('(', ')').forEach[surround[noSpace lowPriority]]
		constraint.formula.allRegionsFor.keywords(':').forEach[surround[oneSpace].append[newLine]]
		constraint.formula.allRegionsFor.keywordPairs('(', ')').forEach[interior[indent]]
		
		constraint.formula.prepend[newLine].surround[indent].format
	}

	def dispatch void format(Get get, extension IFormattableDocument document) {
		get.regionFor.keywords('.', '::').forEach[surround[noSpace]]
		get.next.format
	}
}
